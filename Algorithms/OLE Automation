--Enable OLE Automation Procedures on DB
EXEC master.dbo.sp_configure 'show advanced options', 1
RECONFIGURE
EXEC master.dbo.sp_configure 'Ole Automation Procedures', 1
RECONFIGURE
EXEC sp_configure 'Ole Automation Procedures'; 
GO

--Create new stored Procedure "CreateFolder"
CREATE PROCEDURE [dbo].[CreateFolder] (@newfolder VARCHAR(1000)) AS  
BEGIN  
DECLARE @OLEfolder   INT  
DECLARE @OLEsource   VARCHAR(255)  
DECLARE @OLEdescription  VARCHAR(255) 
DECLARE @init   INT  
DECLARE @OLEfilesytemobject INT  
 
-- it will fail if OLE automation not enabled
EXEC @init=sp_OACreate 'Scripting.FileSystemObject', @OLEfilesytemobject OUT  
IF @init <> 0  
BEGIN  
	EXEC sp_OAGetErrorInfo @OLEfilesytemobject  
	RETURN  
END  
-- check if folder exists  
EXEC @init=sp_OAMethod @OLEfilesytemobject, 'FolderExists', @OLEfolder OUT, @newfolder  
-- if folder doesnt exist, create it  
IF @OLEfolder=0  
	BEGIN  
	EXEC @init=sp_OAMethod @OLEfilesytemobject, 'CreateFolder', @OLEfolder OUT, @newfolder  
END  
-- in case of error, raise it   
IF @init <> 0  
	BEGIN  
		EXEC sp_OAGetErrorInfo @OLEfilesytemobject, @OLEsource OUT, @OLEdescription OUT  
		SELECT @OLEdescription='Could not create folder: ' + @OLEdescription  
		RAISERROR (@OLEdescription, 16, 1)   
	END  
EXECUTE @init = sp_OADestroy @OLEfilesytemobject  
END  
GO 

-- Execute SQL logic to extract documents from the DB to the location assigned to OutPutPath.
-- The below path will be the a location on the remote server hosting the DB where script is run.
-- The outPutPath value path should be set to a location where files will be extracted to.
DECLARE @outPutPath VARCHAR(50) = 'C:\<FilePath>'
, @i BIGINT
, @init INT
, @data VARBINARY(MAX) 
, @fPath VARCHAR(MAX)  
, @folderPath  VARCHAR(MAX) 
 
--Get Data into temp Table variable so that we can iterate over it 
DECLARE @Doctable TABLE (
							 id INT IDENTITY(1,1),
							 GroupNumber VARCHAR(100), 
							 [FileName]  VARCHAR(100), 
							 [Doc_Content] VARBINARY(MAX) 
						 )
 
INSERT INTO @Doctable
			(
				 GroupNumber, 
				 [FileName],
				 [Doc_Content]
			 )

SELECT GroupNumber , Name, [Data]
FROM [Group] G
 

SELECT @i = COUNT(1) 
FROM @Doctable
 
WHILE @i >= 1
BEGIN 
  SELECT 
		@data = [Doc_Content],
		@fPath = @outPutPath + '\'+ [GroupNumber] + '\' +[FileName],
		@folderPath = @outPutPath + '\'+ [GroupNumber]
  FROM  @Doctable 
  WHERE id = @i
 
  --Create folder first
  EXEC dbo.CreateFolder @folderPath
  
  EXEC sp_OACreate 'ADODB.Stream', @init OUTPUT; -- An instance created
  EXEC sp_OASetProperty @init, 'Type', 1;  
  EXEC sp_OAMethod @init, 'Open'; -- Calling a method
  EXEC sp_OAMethod @init, 'Write', NULL, @data; -- Calling a method
  EXEC sp_OAMethod @init, 'SaveToFile', NULL, @fPath, 2; -- Calling a method
  EXEC sp_OAMethod @init, 'Close'; -- Calling a method
  EXEC sp_OADestroy @init; -- Closed the resources
 
  PRINT 'Document Generated at - '+  @fPath   

	--Reset the variables for next use
  SELECT  @data = NULL,  
		  @init = NULL,
		  @fPath = NULL , 
		  @folderPath = NULL
  SET @i -= 1
END
